plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.cloud.tools.jib' version '3.4.5'
    id 'org.openapi.generator' version '7.12.0'
}

group = 'ru.carbohz.payment'
version = 'v3.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.8.5'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'io.projectreactor:reactor-test:3.7.8'
}

tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$projectDir/src/main/resources/api-spec.yaml")
    outputDir.set("$buildDir/generated")
    modelPackage.set("ru.carbohz.payment.api.model")
    apiPackage.set("ru.carbohz.payment.api")
    configOptions.set([
            interfaceOnly: "true",
            library: "spring-boot",
            useTags: "true",
            apiNamePrefix: "Payment",
            apiNameSuffix: "Api",
            reactive: "true"
    ])
}

// Add generated sources to compilation
sourceSets.main.java.srcDirs += "$buildDir/generated/src/main/java"

compileJava.dependsOn tasks.openApiGenerate

jib {
    from {
        image = 'eclipse-temurin:21-jre-jammy' // Base image (Java 21)
    }
    to {
        image = 'carbohz-payment' // Your image name
        tags = ['v3.0'] // Optional tags
    }
    container {
        ports = ['8081'] // Expose port 8080 (Spring Boot default)
        creationTime = 'USE_CURRENT_TIMESTAMP' // Optional
        mainClass = 'ru.carbohz.payment.PaymentApplication'
    }
}

tasks.named('jibDockerBuild') {
    dependsOn(tasks.named('bootJar'))
}