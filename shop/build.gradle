plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.cloud.tools.jib' version '3.4.5'
    id 'org.openapi.generator' version '7.12.0'
}

group = 'ru.carbohz.shop'
version = 'v4.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.liquibase:liquibase-core'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    runtimeOnly 'org.postgresql:r2dbc-postgresql'
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:r2dbc'
    testImplementation 'com.redis:testcontainers-redis:2.2.2'
    testImplementation 'com.github.dasniko:testcontainers-keycloak:3.9.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.jsoup:jsoup:1.20.1'
    testImplementation 'io.projectreactor:reactor-test:3.7.8'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
}

tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = "java"
    inputSpec = "$rootDir/payment/src/main/resources/api-spec.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "ru.carbohz.shop.api"
    modelPackage = "ru.carbohz.shop.api.model"
    configOptions = [
            library: "webclient",
            useJakartaEe: "true",
            openApiNullable: "false"
    ]
}

// Add generated sources to compilation
sourceSets.main.java.srcDirs += "$buildDir/generated/src/main/java"

compileJava.dependsOn tasks.openApiGenerate

jib {
    from {
        image = 'eclipse-temurin:21-jre-jammy' // Base image (Java 21)
    }
    to {
        image = 'carbohz-shop' // Your image name
        tags = ['v4.0'] // Optional tags
    }
    container {
        jvmFlags = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:5005']
        ports = ['8080', '5005'] // Expose port 8080 (Spring Boot default)
        creationTime = 'USE_CURRENT_TIMESTAMP' // Optional
        mainClass = 'ru.carbohz.shop.ShopApplication'
    }
}

tasks.named('jibDockerBuild') {
    dependsOn(tasks.named('bootJar'))
}